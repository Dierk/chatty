apply from: "$rootDir/gradle/integrationTest.gradle"

buildscript {
    repositories {
        jcenter()
    }
}

task gruntTest(type: GruntTask) {
    group = "Verification"
    description = "Runs the Jasmine unit tests."
    gruntArgs = "test"

    inputs.dir "[relative-path-to-source]"
    inputs.file file("Gruntfile.js")
}

task gruntIntegrationTest(type: GruntTask) {
    doFirst {
        // The integration test server finds a free port
        // This port is propagated through the ext.freePort project property
        // To publish it to the JavaScript integration tests, a js file containing
        // the port is created on the fly
        // this file has to be included in the jasmine src config
        def dirs= new File("$project.projectDir/src/generated/js")
        dirs.mkdirs()
        def freePort = project(':com.toedter.chatty.server').freePort
        def portJs = new File("$project.projectDir/src/generated/js/freePort.js")
        if (portJs.exists()) {
            portJs.delete();
        }
        portJs.createNewFile()
        portJs.append("var freePort = " + freePort + ";");
    }

    group = "Verification"
    description = "Runs the Jasmine integration tests."
    gruntArgs = "itest"

    inputs.dir "[relative-path-to-source]"
    inputs.file file("Gruntfile.js")
}

gruntIntegrationTest.dependsOn ':com.toedter.chatty.server:startServerAsync'
gruntIntegrationTest.mustRunAfter ':com.toedter.chatty.server:integrationTest'
gruntIntegrationTest.finalizedBy(':com.toedter.chatty.server:stopServer')

integrationTest.dependsOn gruntIntegrationTest;
test.dependsOn gruntTest;
